# TODO expr, path - escaping
# TODO build - implicit deps, order only deps, implicit outputs

comment = /#[^\n]*$/;
eol = /$\n/;
empty_line = /[\t ]*/ eol;
indent = /[\t ]+/;
indent_opt = /[\t ]*/;

identifier = /[a-zA-Z0-9_.-]+/;

expr = /[^\n]*/;
assign = to:identifier indent "=" indent val:expr eol;

rule = "rule" indent name:identifier indent_opt eol {indent vars:assign}*;

path = /[^\n\t: ]+/;
path_list = indent @+:path {indent @+:path}*;

build = "build" outputs:path_list indent_opt ":" indent_opt rule_name:identifier inputs:path_list indent_opt eol {indent vars:assign}*;

manifest = {empty_line | comment | @+:assign | @+:rule | @+:build}* $;
