@@whitespace :: / +/
@@comments :: /#.*?$/
@@eol_comments :: /#.*?$/
whitespace = / +/;
eol = /$|\n/;

simple_varname = /[a-zA-Z0-9_-]+/;
varname = /[a-zA-Z0-9_.-]+/;

expr = /(\$\n|.)*?$/;
PATH = /((?<!\$)\$\n|\$ |\$:|[^ :|\n])+/;
path_w = PATH;
paths = [@+:PATH {>whitespace @+:PATH}*];

assign = assign:varname "=" value:expr >eol;

SCOPED_VARS = {/  / @+:assign}*;

rule =
	"rule"
	rule:varname
	>eol
	vars:SCOPED_VARS;

build =
	"build"
	targets_explicit: paths
	["|" targets_implicit: paths]
	":"
	build:varname
	inputs_explicit: paths
	["|" inputs_implicit: paths]
	["||" inputs_order: paths]
	/$/
	>eol
	vars:SCOPED_VARS;

default =
	"default"
	defaults:paths /$/
	>eol;

pool =
	"pool"
	pool:varname
	>eol
	vars:SCOPED_VARS;

include =
	"include"
	include:path_w /$/
	>eol;

subninja =
	"subninja"
	subninja:path_w /$/
	>eol;

manifest = {@+:rule | @+:build | @+:default | @+:pool | @+:include | @+:subninja | @+:assign}* $;
